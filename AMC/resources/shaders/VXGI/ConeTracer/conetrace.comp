#version 460 core

#extension GL_ARB_bindless_texture : require
#extension GL_EXT_shader_image_load_formatted : require

#include<..\..\..\resources\shaders\include\CommonTypes.glsl>
#include<..\..\..\resources\shaders\include\Transformations.glsl>
#include<..\..\..\resources\shaders\include\StaticUniformBuffers.glsl>
#include<..\..\..\resources\shaders\include\Surface.glsl>
#include<..\..\..\resources\shaders\include\Random.glsl>
#include<..\..\..\resources\shaders\include\Sampling.glsl>
#include<..\..\..\resources\shaders\include\Ray.glsl>
#include<..\..\..\resources\shaders\include\Box.glsl>
#include<..\..\..\resources\shaders\include\Frustum.glsl>
#include<..\..\..\resources\shaders\include\IntersectionRoutines.glsl>
#include<..\..\..\resources\shaders\include\TraceCone.glsl>

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
layout(binding = 1) uniform sampler3D SamplerVoxels;
layout(binding = 2) restrict writeonly uniform image2D ImgResult;

layout(location = 0) uniform int MaxSamples;
layout(location = 1) uniform float StepMultiplier;
layout(location = 2) uniform float GIBoost;
layout(location = 3) uniform float GISkyBoxBoost;
layout(location = 4) uniform float NormalRayOffset;

vec3 IndirectLight(Surface surface, vec3 position, vec3 incomming);
float GetMaterialVariance(float specularChance, float roughness);

void main()
{
    ivec2 imgCoord = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv = (imgCoord + 0.5) / imageSize(ImgResult);

    float depth = texelFetch(gBufferDataUBO.Depth, imgCoord, 0).r;
    if (depth == 1.0)
    {
        imageStore(ImgResult, imgCoord, vec4(0.0));
        return;
    }

    vec3 fragPos = PerspectiveTransformUvDepth(vec3(uv, depth), perFrameDataUBO.InvProjView);
    vec3 normal = normalize(texelFetch(gBufferDataUBO.Normal, imgCoord, 0).rgb);
    float specular = texelFetch(gBufferDataUBO.MetallicRoughness, imgCoord, 0).r;
    float roughness = texelFetch(gBufferDataUBO.MetallicRoughness, imgCoord, 0).g;

    Surface surface = GetDefaultSurface();
    surface.Metallic = specular;
    surface.Roughness = roughness;
    surface.Normal = normal;

    vec3 viewDir = fragPos - perFrameDataUBO.ViewPos;
    vec3 indirectLight = IndirectLight(surface, fragPos, viewDir) * GIBoost;

    imageStore(ImgResult, imgCoord, vec4(indirectLight, 1.0));
}

vec3 IndirectLight(Surface surface, vec3 position, vec3 incomming)
{    
    surface.Roughness *= surface.Roughness; // just a convention to make roughness feel more linear perceptually
    
    vec3 irradiance = vec3(0.0);
    float materialVariance = GetMaterialVariance(surface.Metallic, surface.Roughness);
    uint samples = uint(mix(1.0, float(MaxSamples), materialVariance));

    uint noiseIndex = 0;
    for (uint i = 0; i < samples; i++)
    {
        float rnd0 = InterleavedGradientNoise(vec2(gl_GlobalInvocationID.xy), noiseIndex + 0);
        float rnd1 = InterleavedGradientNoise(vec2(gl_GlobalInvocationID.xy), noiseIndex + 1);
        float rnd2 = InterleavedGradientNoise(vec2(gl_GlobalInvocationID.xy), noiseIndex + 2);
        noiseIndex++;
        
        Ray coneRay;
        coneRay.Origin = position;
        coneRay.Direction;

        vec3 diffuseDir = CosineSampleHemisphere(surface.Normal, rnd0, rnd1);
        
        const float maxConeAngle = 0.32;  // 18 degree
        const float minConeAngle = 0.005; // 0.29 degree
        float coneAngle;
        if (surface.Metallic > rnd2)
        {
            vec3 reflectionDir = reflect(incomming, surface.Normal);
            reflectionDir = normalize(mix(reflectionDir, diffuseDir, surface.Roughness));
            coneRay.Direction = reflectionDir;
            
            coneAngle = mix(minConeAngle, maxConeAngle, surface.Roughness);
        }
        else
        {
            coneRay.Direction = diffuseDir;
            coneAngle = maxConeAngle;
        }

        vec4 coneTrace = TraceCone(SamplerVoxels, coneRay, surface.Normal, coneAngle, StepMultiplier, NormalRayOffset, 0.99);
        coneTrace += (1.0 - coneTrace.a) * (texture(skyBoxUBO.Albedo, coneRay.Direction) * GISkyBoxBoost);
        
        irradiance += coneTrace.rgb;
    }
    irradiance /= float(samples);

    return irradiance;
}

float GetMaterialVariance(float specularChance, float roughness)
{
    float diffuseChance = 1.0 - specularChance;
    float perceivedFinalRoughness = 1.0 - (specularChance * (1.0 - roughness));
    return mix(perceivedFinalRoughness, 1.0, diffuseChance);
}

